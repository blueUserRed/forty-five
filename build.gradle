buildscript {
    ext.kotlinVersion = '1.7.0'
    ext.gdxVersion = '1.11.0'
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        classpath 'edu.sc.seis.launch4j:launch4j:2.5.4'
    }
}

allprojects {
    apply plugin: "eclipse"

    ext {
        appName = "TestGame"
        gdxVersion = '1.11.0'
        roboVMVersion = '2.3.16'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url 'https://jitpack.io' }
    }
}

project(":desktop") {
    apply plugin: "kotlin"
    apply plugin: 'java'
    apply plugin: 'edu.sc.seis.launch4j'

    repositories {
        jcenter()
    }


    launch4j {
        mainClassName = 'com.fourinachamber.fortyfive.DesktopLauncher'
        icon = "${projectDir}/icons/myApp.ico"
        headerType = "gui"
    }
    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":onj") {
    apply plugin: "kotlin"
    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-reflect:1.8.0")
    }
}

project(":core") {
    apply plugin: "kotlin"

    dependencies {
        implementation project(":onj")
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api 'io.github.libktx:ktx-actors:1.11.0-rc2'
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
        api "com.github.lyze237:gdx-FlexBox:425149b588"
        api "com.github.lyze237:gdx-FlexBox:425149b588:sources"
    }
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker
import java.io.File

task texturePacker {
    doLast {
        file("assets/textures/toPack.txt").eachLine { line ->
            if (!line.allWhitespace) {

                def atlasDir = file("assets/textures/packed/$line")
                if (atlasDir.exists()) {
                    atlasDir.deleteDir()
                }
                println(line)
                TexturePacker.process(
                        "assets/textures/$line",
                        "assets/textures/packed/$line",
                        line
                )
            }
        }
    }
}

def packAnimation(anim) {
    def atlasDir = file("assets/large_assets/packed/$anim")
    if (atlasDir.exists()) {
        atlasDir.deleteDir()
    }
    TexturePacker.process(
        "assets/large_assets/$anim",
        "assets/large_assets/packed/$anim",
        anim
    )
}

task packAnimations {
    doLast {
        packAnimation("outlaw_animation")
        packAnimation("pyro_animation")
        packAnimation("sheep_animation")
    }
}
